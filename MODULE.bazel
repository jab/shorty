bazel_dep(name = "rules_python", version = "0.33.1")
bazel_dep(name = "aspect_rules_py", version = "0.7.3")
bazel_dep(name = "aspect_rules_lint", version = "1.0.0-rc3")
bazel_dep(name = "rules_multitool", version = "0.7.1")
bazel_dep(name = "buildifier_prebuilt", version = "6.3.3")
bazel_dep(name = "rules_uv", version = "999.999.999")

# Pick up fix for https://github.com/theoremlp/rules_uv/issues/58 while it's not yet released:
git_override(
    module_name = "rules_uv",
    commit = "b2b4b268514301f55912f6781c43e1bac82a1ad3",
    remote = "https://github.com/theoremlp/rules_uv.git",
)

PY_VER = "3.12"

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    configure_coverage_tool = True,
    is_default = True,
    python_version = PY_VER,
)
use_repo(python, "python_{}".format(PY_VER.replace(".", "_")))

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")

[
    pip.parse(
        hub_name = "pip_{}_deps".format(i),
        python_version = PY_VER,
        requirements_lock = "//requirements:{}.txt".format(i),
    )
    for i in ("base", "test", "dev")
]

use_repo(pip, "pip_base_deps", "pip_test_deps", "pip_dev_deps")

#####################################################################################
# For linters, formatters, and other tools:
multitool = use_extension("@rules_multitool//multitool:extension.bzl", "multitool")
multitool.hub(lockfile = "//tools:tools.lock.json")
use_repo(multitool, "multitool")

#####################################################################################
# For building an efficient container image:
bazel_dep(name = "rules_oci", version = "1.7.5")
bazel_dep(name = "aspect_bazel_lib", version = "2.7.6")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "ubuntu",
    digest = "sha256:2e863c44b718727c860746568e1d54afd13b2fa71b160f5cd9058fc436217b30",
    image = "ubuntu",
    platforms = [
        "linux/amd64",
        "linux/arm/v7",
        "linux/arm64/v8",
        "linux/ppc64le",
        "linux/s390x",
    ],
)
use_repo(oci, "ubuntu")

# Depend on a hermetic cc toolchain, which is required to build a container image.
bazel_dep(name = "hermetic_cc_toolchain", version = "3.1.0")

toolchains = use_extension("@hermetic_cc_toolchain//toolchain:ext.bzl", "toolchains")
use_repo(toolchains, "zig_sdk")

register_toolchains("@zig_sdk//toolchain:linux_amd64_gnu.2.31")
