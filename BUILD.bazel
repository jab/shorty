load("@pip_base_deps//:requirements.bzl", pip_base_deps = "all_requirements")
load("@pip_test_deps//:requirements.bzl", pip_test_deps = "all_requirements")
load("@rules_uv//uv:pip.bzl", "pip_compile")
load("@rules_python//python:defs.bzl", "py_library", "py_test")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")

filegroup(
    name = "templates",
    srcs = glob(["templates/*.jinja"]),
)

py_library(
    name = "app",
    srcs = ["app.py"],
    data = [":templates"],
    deps = pip_base_deps,
)

py_test(
    name = "test_app",
    srcs = ["test_app.py"],
    deps = [":app"] + pip_base_deps + pip_test_deps,
)

[
    py_console_script_binary(
        name = i,
        pkg = "@pip_base_deps//{}".format(i),
        deps = [":app"],
    )
    for i in ("flask", "hypercorn")
]

[
    pip_compile(
        name = "compile_{}_requirements".format(i),
        requirements_in = "requirements.{}.in".format(i),
        requirements_txt = "requirements.{}.txt".format(i),
        args = ["--no-build"],
    )
    for i in ("base", "test")
]

exports_files([".ruff.toml"])
